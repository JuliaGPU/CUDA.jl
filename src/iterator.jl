export CuIterator

"""
    CuIterator(batches)

Return a `CuIterator` that can iterate through the provided `batches` via `Base.iterate`.

Upon each iteration, the current `batch` is adapted to the GPU (via `map(x -> adapt(CuArray, x), batch)`)
and the previous iteration is marked as freeable from GPU memory (via `unsafe_free!`).

This abstraction is useful for batching data into GPU memory in a manner that
allows old iterations to potentially be freed (or marked as reusable) earlier
than they otherwise would via CuArray's internal polling mechanism.
"""
mutable struct CuIterator{B}
    batches::B
    previous::Any
    CuIterator(batches) = new{typeof(batches)}(batches)
end

function Base.iterate(c::CuIterator, state...)
    item = iterate(c.batches, state...)
    isdefined(c, :previous) && foreach(unsafe_free!, c.previous)
    item === nothing && return nothing
    batch, next_state = item
    cubatch = map(x -> adapt(CuArray, x), batch)
    c.previous = cubatch
    return cubatch, next_state
end

Base.IteratorSize(::Type{CuIterator{B}}) where {B} = Base.IteratorSize(B)
Base.length(c::CuIterator) = length(c.batches)  # required for HasLength
Base.axes(c::CuIterator) = axes(c.batches)  # required for HasShape{N}

Base.IteratorEltype(::Type{CuIterator{B}}) where {B} = Base.IteratorEltype(B)
Base.eltype(c::CuIterator) = eltype(c.batches)  # required for HasEltype

